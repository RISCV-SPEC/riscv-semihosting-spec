== RISC-V Semihosting Binary Interface

The RISC-V semihosting binary interface consist of a breakpoint instruction
sequence and a mechanism to pass parameters which are defined by the
following sub-sections.

=== Semihosting Breakpoint Instruction Sequence

Semihosting operations are requested using a sequence of instructions
including `ebreak`. Because the RISC-V base ISA does not provide more than
one `ebreak` instruction, RISC-V semihosting uses a special sequence of
instructions to distinguish a semihosting `ebreak` from a debugger inserted
`ebreak`. The <<breakpoint_insns>> shows the instruction sequence used to
invoke a semihosting operation.

.RISC-V Semihosting Breakpoint Sequence
[#breakpoint_insns]
----
slli x0, x0, 0x1f   # 0x01f01013   Entry NOP
ebreak              # 0x00100073   Break to debugger
srai x0, x0, 7      # 0x40705013   Exit NOP
----

These three instructions must be 32-bit wide instructions. This same sequence
is used on all RISC-V architectures. If paging is in use in the current mode,
this sequence must not cross a page boundary as the semihosting system must
be able to check for the semihosting sequence without needing data from
potentially missing pages. The <<function>> shows how this can be done by
placing the sequence in a separate assembly function and aligning that to
prevent that from spanning a page boundary.

.RISC-V Semihosting Assembly Reference Function
[#function]
----
        .option norvc
        .text
        .balign 16
        .global sys_semihost
        .type sys_semihost @function
sys_semihost:
        slli zero, zero, 0x1f
        ebreak
        srai zero, zero, 0x7
        ret
----

=== Semihosting Parameters

The type of semihosting operation and its parameters are specified using
general purpose registers. The OPERATION NUMBER is specified in the `a0`
register and the PARAMETER is specified in the `a1` register whereas the
RETURN VALUE is available in the `a0` register. All registers and data block
fields are XLEN wide.
